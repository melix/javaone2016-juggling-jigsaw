repositories {
    jcenter()
}

dependencies {
    compile project(':utils')
}

platforms {
   targetPlatforms 'java7', 'java8', 'java9'
}

task multireleaseJar(type:Jar) {
    dependsOn compileJavaJava7, compileJavaJava9
    baseName = "${project.name}-multi"
    from compileJavaJava7.destinationDir
    into('META-INF/versions/9') {
       from compileJavaJava9.destinationDir
    }
    manifest.attributes('Multi-Release': 'true')
}

task runMr {}
task run {}

platforms.targetPlatforms.each { platform ->
    def platformRun = task "${platform}RunMrJar"(type: JavaExec) {
        dependsOn multireleaseJar
        classpath = files(multireleaseJar.archivePath,
                          configurations."runtime${platform.capitalize()}")
        executable = "${platforms.jdkFor(platform)}/bin/java"
        main = 'com.acme.core.Main'
    }
    runMr.dependsOn(platformRun)
    def platformJar = tasks.findByName("${platform}Jar")
    def platformRun2 = task "${platform}Run"(type: JavaExec) {
        dependsOn platformJar
        classpath = files(platformJar.archivePath, configurations."runtime${platform.capitalize()}")
        executable = "${platforms.jdkFor(platform)}/bin/java"
        main = 'com.acme.core.Main'
        doFirst { println classpath.asPath }
    }
    run.dependsOn(platformRun2)
}

